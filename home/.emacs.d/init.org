* Introduction
There is a stub [[file:~/.emacs][.emacs file]] which uses org-babel to load this file.

Inspiration from the approach at [[https://github.com/dbr/dotemacs][dbr/dotemacs]] as found in a [[http://www.reddit.com/r/emacs/comments/12pgtg/restarting_from_scratch/][reddit thread]].

* Package Management
** Package setup
[[file:~/.homesick/repos/dot-emacs/home/.emacs::%3B%3B%20Load%20up%20org-mode%20and%20org-babel.][Please see your .emacs file.]]
** Package install
And now for automatic package installation:
#+begin_src emacs-lisp
  (defun ensure-pkg (pkg)
    (unless (package-installed-p pkg)
      (package-install pkg)))

  (defun ensure-pkgs (packages)
    (mapc 'ensure-pkg packages))

  (defmacro when-installed (pkg &rest body)
    `(when (package-installed-p ,pkg)
       ,@body))
#+end_src
* Behavior Tweaks
** cl
#+begin_src emacs-lisp
(require 'cl)
#+end_src

** s
String libray, used in some of my yasnippets.
[[https://github.com/magnars/s.el][Github link.]]
#+begin_src emacs-lisp
  (ensure-pkg 's)
  (require 's)
#+end_src
** Server mode
#+begin_src emacs-lisp
  (when (eq system-type 'darwin)
    (server-force-delete)
    (setq server-socket-dir (format "/tmp/emacs%d" (user-uid)))
    (server-start))
#+end_src
** Chdir to ~
Depending on how the emacs daemon is started, the current directory is less than ideal.
#+begin_src emacs-lisp
  (cd (expand-file-name "~/"))
#+end_src
** Disable backups
#+begin_src emacs-lisp
  (setq backup-inhibited t)
  (setq auto-save-default nil)
#+end_src
** But use a better auto-save mode
#+begin_src emacs-lisp
  (ensure-pkg 'auto-save-buffers-enhanced)
  (setq auto-save-buffers-enhanced-interval 5)  ; Auto-save on 5 second idle.
  (auto-save-buffers-enhanced-include-only-checkout-path t)  ; Only auto-save VCS tracked files.
  (auto-save-buffers-enhanced t)
#+end_src
** Speed Tweaks
#+begin_src emacs-lisp
(setq font-lock-verbose nil)
(setq vc-handled-backends '(Git SVN))
;(setq jit-lock-defer-time 0.05)
(setq gc-cons-threshold 20000000)
#+end_src
** Modern Sentences
#+begin_src emacs-lisp
(setq sentence-end-double-space nil)
#+end_src
** Zap chars
I prefer the vim style zap.  Leaving old one on M-Z.
#+begin_src emacs-lisp
(autoload 'zap-up-to-char "misc"
  "Kill up to, but not including ARGth occurrence of CHAR.")
(global-set-key (kbd "M-z") 'zap-up-to-char)
(global-set-key (kbd "M-Z") 'zap-to-char)
#+end_src
** Save point position between sessions
#+begin_src emacs-lisp
(require 'saveplace)
(setq-default save-place t)
(setq save-place-file (expand-file-name ".places" user-emacs-directory))
#+end_src
** Mouse support on terminal
#+BEGIN_SRC emacs-lisp
  (xterm-mouse-mode)
#+END_SRC
** Uniquify
#+begin_src emacs-lisp
  (require 'uniquify)
  (setq uniquify-buffer-name-style 'forward)
#+end_src
** xterm keys
#+begin_src emacs-lisp
  (defun my-terminal-hook ()
    (define-key key-translation-map (kbd "<select>") (kbd "<end>")))
  (add-hook 'tty-setup-hook 'my-terminal-hook)
#+end_src
** Follow vc links
Doesn't work.  Odd.
#+begin_src emacs-lisp
  (setq vc-follow-symlinks t)
#+end_src
** TODO Get path from a shell
Doesn't seem to work reliably.
#begin_src emacs-lisp
  ;(ensure-pkg 'exec-path-from-shell)
  ;(exec-path-from-shell-initialize)
#end_src
** Key Chords?
#+begin_src emacs-lisp
  (ensure-pkg 'key-chord)
  (require 'key-chord)
  (key-chord-mode 1)
#+end_src
** Disable cua-mode, mostly.
#+begin_src emacs-lisp
  (setq cua-enable-cua-keys nil)
#+end_src
* Interface
** Maximize frame
Needs to be run only on darwin
#+begin_src emacs-lisp
  (when (eq system-type 'darwin)
    (ensure-pkgs '(maxframe))
    (require 'maxframe)
    (setq mf-display-padding-height 60)  ; was 45
    (setq fudge-maximize-frame-first t)
    (defun fudge-maximize-frame (frame)
      (if fudge-maximize-frame-first
          (run-with-idle-timer 0.5 nil 'maximize-frame frame)
        (maximize-frame frame)
        (setq fudge-maximize-frame-first nil)))
    ;; (add-hook 'server-visit-hook 'fudge-maximize-frame t)
    (add-hook 'server-visit-hook 'raise-frame t)
    (add-to-list 'after-make-frame-functions 'fudge-maximize-frame))
#+end_src

#+begin_src emacs-lisp
  ;; Extra strength, for macs.
  (if (featurep 'ns)
      (progn
        (defun ns-raise-emacs ()
          "Raise Emacs."
          (if (display-graphic-p)
              (ns-do-applescript "tell application \"Emacs\" to activate")))

        (add-hook 'server-visit-hook 'ns-raise-emacs)
        (add-hook 'before-make-frame-hook 'ns-raise-emacs)))
#+end_src

** Font
#+begin_src emacs-lisp
  (when (eq system-type 'darwin)
    (set-face-attribute 'default nil
    :family "Sauce Code Powerline" :height 145 :weight 'medium))
#+end_src
#+begin_src emacs-lisp
  (ensure-pkg 'dynamic-fonts)
  (require 'dynamic-fonts)
  (add-to-list 'dynamic-fonts-preferred-monospace-fonts "Saurce Code Powerline")
  (setq dynamic-fonts-preferred-monospace-point-size 14)
  (setq dynamic-fonts-preferred-proportional-point-size 14)
  (dynamic-fonts-setup)
#+end_src
** Mac command modifiers
#+begin_src emacs-lisp
(setq mac-command-modifier 'hyper)
(setq mac-option-modifier 'meta)
(setq mac-fn-modifier 'super)
#+end_src
** Mac Kill Frame Hook
#+begin_src emacs-lisp
  (when (featurep 'ns)
      (defun ns-destroy-frame (frame)
        (if (display-graphic-p)
            (ns-do-applescript "tell application \"Finder\" to set visible of process \"Emacs\" to false")))
      (add-to-list 'delete-frame-functions 'ns-destroy-frame))

#+end_src

** Minimal Window Decor
#+begin_src emacs-lisp
  (setq inhibit-splash-screen t)
  (unless (eq system-type 'darwin)
    (menu-bar-mode 0))
  (tool-bar-mode 0)
  (if (boundp 'scroll-bar-mode)
      (scroll-bar-mode 0))
#+end_src
** Color Theme
I'm using a forked solarized version which supports terminals with 256 colors, but with the normal 16 configured in
the solarized theme.  The available packages currently use the 256 color version, which isn't as accurate.
#+begin_src emacs-lisp
;(add-to-list 'custom-theme-load-path (expand-file-name "~/.emacs.d/non-elpa/solarized"))
;(load-theme 'solarized-dark t)
#+end_src

#+begin_src emacs-lisp
;  (ensure-pkg 'color-theme-approximate)
;  (color-theme-approximate-on)
#+end_src

Let's try the most comprehensive one again, but be careful to always
tell it we have a 16 color terminal, not 256.

#begin_src emacs-lisp

;  (ensure-pkg 'solarized-theme)

;  (defun my-after-make-frame (frame)
;    (cond ((display-graphic-p frame)
;           (message "Graphical frame")
;           (load-theme 'solarized-dark t))
;          (t (message "Not a graphical frame")
;             (disable-theme 'solarized-dark))))
;
;  (add-hook 'after-make-frame-functions 'my-after-make-frame)
;  (ensure-pkg 'solarized-theme)
;  (load-theme 'solarized-dark t)
#end_src

#+begin_src emacs-lisp
;;  (ensure-pkg 'color-theme-sanityinc-solarized)
;;  (require 'color-theme-sanityinc-solarized)
;;  (color-theme-sanityinc-solarized-dark)
#+end_src

#begin_src emacs-lisp
  (ensure-pkg 'zenburn-theme)
  (load-theme 'zenburn t)
#end_src

This is *just* enough shittiness to get the themes to work correctly for the first frame launched under an emacs daemon.
The problem is that emacs as a daemon has an invisible frame, which is why any color theme which tries to look at the display or frame to get
information craps out.

So I delay theme setting until *after* frame creation and put a little async on it.

#+begin_src emacs-lisp
  (ensure-pkg 'dash)
  (require 'dash)
  (ensure-pkg 'darktooth-theme)
  (defvar my-set-theme nil)
  (defvar my-theme 'darktooth)

  (defun my-fake-frame-p (frame)
    (message "Looking at %s" (terminal-name frame))
    (string-equal "initial_terminal" (terminal-name frame)))

  (defun my-after-make-frame (frame)
    (if my-set-theme (message "Theme already set")
      (cond ((not (my-fake-frame-p frame))
             (message "Real frame (after make)")
             (run-at-time 0.3 nil 'load-theme my-theme t)
             (setq my-set-theme t))
            (t (message "Daemon frame. (after make)")))))

  ;;; Might not need this one.
  (defun my-after-switch-server ()
    (if my-set-theme (message "Theme already set")
      (cond ((-remove 'my-fake-frame-p (visible-frame-list))
             (message "Real frame (switch server)")
             (load-theme my-theme t)
             (setq my-set-theme t))
            (t (message "Daemon frame (switch server)")))))

  (add-hook 'after-make-frame-functions 'my-after-make-frame)
  (add-hook 'server-switch-hook 'my-after-switch-server)
#+end_src
** Don't bother me as much
#+begin_src emacs-lisp
(defalias 'yes-or-no-p 'y-or-n-p)
#+end_src
** Hydra
#+begin_src emacs-lisp
  (ensure-pkg 'hydra)
#+end_src
*** Custom svin hydra
#+begin_src emacs-lisp
  (ensure-pkg 'with-editor)
  (require 'vc-dir)
  (defun my-default-directory-no-slash ()
    (if (string-match "/$" default-directory)
        (replace-match "" t t default-directory)))

  (defun my-svin-review-id ()
    (let ((reviewboard-json (json-read-file (expand-file-name "~/.svin/reviewboard/rb_record.json"))))
      (cdr (assoc 'default (cdr (assoc (intern (my-default-directory-no-slash)) reviewboard-json))))))

  (defun my-svin-create-review (group)
    (interactive (list (ido-completing-read "Review group?" my-reviewer-groups)))
    (with-editor
      (compile (concat "svin review -r " user-login-name " -g " group))))  ; user-login-name works around a current bug here, and can go away soon.

  (defun my-svin-open-review ()
    (interactive)
    (browse-url (format my-review-url-format (my-svin-review-id))))

  (defun my-svin-update-review ()
    (interactive)
    (with-editor
      (compile (concat  "svin review -r " user-login-name " --update " (my-svin-review-id))))) ; user-login-name works around a current bug here, and can go away soon.

  (defun my-svin-commit-review ()
    (interactive)
    (with-editor (compile (concat "svin commit -r " (my-svin-review-id)))))


  (defhydra hydra-svin-review (:exit t)
    "Review"
    ("c" my-svin-create-review "create")
    ("o" my-svin-open-review "open")
    ("u" my-svin-update-review "update")
    ("d" my-svin-commit-review "commit"))
  (define-key vc-dir-mode-map (kbd ".") 'hydra-svin-review/body)
#+end_src
* Custom Functionality
** Use custom browser script if possible.
#+begin_src emacs-lisp
  (defun browse-url-remote-open (url &optional ignored)
    (interactive (browse-url-interactive-arg "URL: "))
    (if window-system
        (browse-url-default-browser url)
      (call-process "ro" nil 0 nil url)))

  (setq browse-url-browser-function 'browse-url-remote-open)
#+end_src
** Jump to .emacs
#+begin_src emacs-lisp
  (defun my-edit-dot-emacs ()
    (interactive)
    (find-file "~/.emacs.d/init.org"))
  (global-set-key (kbd "C-c e") 'my-edit-dot-emacs)
#+end_src
* Module Configuration
** Iedit                                                      :disabled:
#+begin_src emacs-lisp
  (ensure-pkgs '(iedit))
  (require 'iedit)
  (global-set-key (kbd "C-c i") 'iedit-mode)
#+end_src
** Multiple Cursors                                                :disabled:
Consider instead of Iedit?
#begin_src emacs-lisp
  (ensure-pkg 'multiple-cursors)
  (global-set-key (kbd "C-c ;") 'mc/mark-all-dwim)
#end_src
** Visual Regex
#+begin_src emacs-lisp
  (ensure-pkg 'visual-regexp)
  (global-set-key (kbd "C-c r") 'vr/replace)
  (global-set-key (kbd "C-c q") 'vr/query-replace)
  ;; if you use multiple-cursors, this is for you:
  ;(global-set-key (kbd "C-c R") 'vr/mc-mark)
#+end_src
** Helm
#+BEGIN_SRC emacs-lisp
  (ensure-pkgs '(helm))
  (require 'helm-config)
  (setq helm-input-idle-delay 0.01
        helm-idle-delay helm-input-idle-delay)
  (global-set-key (kbd "M-x") 'helm-M-x)
  (global-set-key (kbd "C-x C-f") 'helm-find-files)
  (global-set-key (kbd "C-x b") 'helm-for-files)

  (key-chord-define-global "xf" 'helm-find-files)
  (key-chord-define-global "bb" 'helm-buffers-list)
  ;(global-set-key (kbd "C-x b") 'switch-to-buffer)
  (helm-mode)
#+END_SRC
*** Swoop
#+begin_src emacs-lisp
  (ensure-pkg 'helm-swoop)
  (require 'helm-swoop)
  (global-set-key (kbd "M-i") 'helm-swoop)
  (global-set-key (kbd "M-I") 'helm-multi-swoop)
  (define-key isearch-mode-map (kbd "M-i") 'helm-swoop-from-isearch)
  (define-key helm-swoop-map (kbd "M-i") 'helm-multi-swoop-all-from-helm-swoop)
  (setq helm-multi-swoop-edit-save t)
#+end_src
*** Dash                                                           :disabled:
#+begin_src emacs-lisp
  (ensure-pkg 'helm-dash)
  (global-set-key (kbd "C-c d") 'helm-dash)
  ;(setq helm-dash-common-docsets '("Python 2"))
#+end_src
** Tramp
Fix too long TMPDIR:
#+begin_src emacs-lisp
(setenv "TMPDIR" "/tmp")
#+end_src

Default method:
#+begin_src emacs-lisp
  ;;(setq tramp-default-method "ssh")
#+end_src

Use remote PATH?
#+begin_src emacs-lisp
  (require 'tramp)
  (add-to-list 'tramp-remote-path 'tramp-own-remote-path)
#+end_src

Cache passwords
#+begin_src emacs-lisp
  (setq password-cache-expiry nil)
#+end_src

Enable remote dir-locals.
#+begin_src emacs-lisp
  (setq enable-remote-dir-locals t)
#+end_src
** Ibuffer
#+begin_src emacs-lisp
  (global-set-key (kbd "C-x C-b") 'ibuffer)
  (autoload 'ibuffer "ibuffer" "List buffers." t)
  (ensure-pkg 'ibuffer-vc)
  (eval-after-load 'ibuffer
    '(progn
       (add-hook 'ibuffer-hook
                 (lambda ()
                   (ibuffer-vc-set-filter-groups-by-vc-root)
                   (unless (eq ibuffer-sorting-mode 'alphabetic)
                     (ibuffer-do-sort-by-alphabetic))))
       (setq ibuffer-formats
             '((mark modified read-only vc-status-mini " "
                     (name 18 18 :left :elide)
                     " "
                     (size 9 -1 :right)
                     " "
                     (mode 16 16 :left :elide)
                     " "
                     (vc-status 16 16 :left)
                     " "
                     filename-and-process)))))
#+end_src
** Window Management
*** Winner Mode
Use the mouse back/forward buttons to go back and forth in history of window configurations.
#+begin_src emacs-lisp
(when (fboundp 'winner-mode)
  (winner-mode 1)
  (global-set-key (kbd "<mouse-8>") 'winner-undo)
  (global-set-key (kbd "<mouse-9>") 'winner-redo))
#+end_src
*** Ace-window
#+begin_src emacs-lisp
  (ensure-pkg 'ace-window)
  (global-set-key (kbd "C-c w") 'ace-window)
  (setq aw-dispatch-always nil)
  (ace-window-display-mode)

#+end_src
** Avy
#+begin_src emacs-lisp
  (ensure-pkg 'avy)
  (global-set-key (kbd "M-g g") 'avy-goto-line)
  (global-set-key (kbd "M-g M-g") 'avy-goto-line)
  (global-set-key (kbd "C-c j") 'avy-goto-char-2)

  ;; Add C-c j in isearch for jump to matches
  (define-key isearch-mode-map (kbd "C-c j") 'avy-isearch)

  (key-chord-define-global "jk" 'avy-goto-char-2)
#+end_src
** Sunrise Commander
#+begin_src emacs-lisp
  (ensure-pkg 'sunrise-commander)
#+end_src
*** Bindings
#+begin_src emacs-lisp
  ;; F11 for sunrise commander
  (global-unset-key (kbd "<f11>"))
  (global-set-key (kbd "<f11>") 'sunrise)
  ;; Safe alternative
  (global-set-key (kbd "C-c s") 'sunrise)
#+end_src
** Magit
#+begin_src emacs-lisp
  (ensure-pkg 'magit)
  (ensure-pkg 'magit-svn)
  ;;(ensure-pkg 'magit-commit-training-wheels)

  ;; Mac uses this homebrew one, which doesn't get found.
  ;; Might go away with exec-path fixes.
  (when (featurep 'ns)
    (setq magit-emacsclient-executable "/usr/local/bin/emacsclient"))
#+end_src
*** Bindings
*** git-review
#+begin_src emacs-lisp
  (require 'magit-popup)
  (require 'magit)

  ;; Customize me:
  (setq magit-reviewer-groups '("mp-dev"))

  (defun magit-review-open-link ()
    (interactive)
    (unless (magit-process-buffer)
      (error "No Git commands have run"))
    (save-excursion
      (magit-process)
      (goto-char (point-min))
      (if (search-forward-regexp "https.*" nil t)
          (browse-url-at-point)
        (message "Did not find url"))))

  (defun magit-review-create (group)
    (interactive (list (ido-completing-read "Review group?" magit-reviewer-groups)))
    (magit-run-git "review" "create" "--no-prompt"
                   "-b" (magit-get-current-branch)
                   "--groups" group
                   "--owners")
    (magit-review-open-link))

  (defun magit-review-dcommit ()
    (interactive)
    (magit-run-git "review" "dcommit"))

  (defun magit-review-open ()
    (interactive)
    (let* ((branch (magit-get-current-branch))
           (reviewid (magit-get (format "branch.%s.reviewid" branch))))
      (browse-url (format my-review-url-format reviewid))))

  (defun magit-review-update ()
    (interactive)
    (magit-run-git "review" "update")
    (magit-review-open-link))

  (magit-define-popup magit-review-popup
    "Popup console for review commands."
    'magit-commands nil nil
    :actions  '((?c "Create"  magit-review-create)
                (?u "Update"  magit-review-update)
                (?o "Open"    magit-review-open)
                (?d "dcommit" magit-review-dcommit))
    :default-action 'magit-review-create)

  (magit-define-popup-action 'magit-dispatch-popup ?. "Reviews" 'magit-review-popup)
  (define-key magit-mode-map "." 'magit-review-popup)
#+end_src
#+begin_src emacs-lisp
;; F12 for magit
(global-unset-key (kbd "<f12>"))
(global-set-key (kbd "<f12>") 'magit-status)
;; Safe alternative
(global-set-key (kbd "C-c g") 'magit-status)
#+end_src
*** Training wheels
#+BEGIN_SRC emacs-lisp
;;(require 'magit-commit-training-wheels)
;;(ad-activate 'magit-log-edit-commit)
#+END_SRC
*** Window advice                                                  :disabled:
From what the emacs.d
#+begin_src emacs-lisp
  (defadvice magit-status (around magit-fullscreen activate)
    (window-configuration-to-register :magit-fullscreen)
    ad-do-it
    (delete-other-windows))

  (if (boundp 'magit-quit-window)
      (defadvice magit-quit-window (after magit-restore-screen activate)
        (jump-to-register :magit-fullscreen)))
  ;; Newer magit:
  (if (boundp 'magit-mode-quit-window)
      (defadvice magit-quit-window (after magit-restore-screen activate)
        (jump-to-register :magit-fullscreen)))
#+end_src
*** Magit SVN                                                      :disabled:
#begin_src emacs-lisp
  (ensure-pkgs '(magit-svn))
  (require 'magit-svn)

  (add-hook 'magit-mode-hook (lambda()
                               (if (magit-svn-get-ref-info)
                                   (magit-svn-mode))))
#end_src

*** Git-Review bindings under Magit SVN                            :disabled:
#begin_src emacs-lisp
  (setq magit-reviewer-groups my-reviewer-groups)

  (defun magit-review-open-link ()
    (interactive)
    (unless (get-buffer magit-process-buffer-name)
      (error "No Git commands have run"))
    (save-excursion
      (set-buffer magit-process-buffer-name)
      (goto-char (point-min))
      (if (search-forward-regexp "https.*" nil t)
          (browse-url-at-point)
        (message "Did not find url"))))

  (defun magit-review-create (group)
    (interactive (list (ido-completing-read "Review group?" magit-reviewer-groups)))
    (magit-run-git "review" "create"
                   "-b" (magit-get-current-branch)
                   "--groups" group)
    (magit-review-open-link))

  (defun magit-review-dcommit ()
    (interactive)
    (magit-run-git "review" "dcommit"))

  (defun magit-review-open ()
    (interactive)
    (let* ((branch (magit-get-current-branch))
           (reviewid (magit-get (format "branch.%s.reviewid" branch))))
      (browse-url (format my-review-url-format reviewid))))

  (defun magit-review-update ()
    (interactive)
    (magit-run-git "review" "update")
    (magit-review-open-link))

  (magit-key-mode-insert-action 'svn "R" "Create Review" 'magit-review-create)
  (magit-key-mode-insert-action 'svn "U" "Update Review" 'magit-review-update)
  (magit-key-mode-insert-action 'svn "D" "Review dcommit" 'magit-review-dcommit)
  (magit-key-mode-insert-action 'svn "O" "Open review" 'magit-review-open)
#end_src

*** Magit-wip                                                      :disabled:
#+begin_src emacs-lisp
  ;(require 'magit-wip)
  ;(global-magit-wip-save-mode 1)
#+end_src
** TODO Battery life in mode line                                  :disabled:
Make mac only.
#+begin_src emacs-lisp
;; (setq battery-mode-line-format "[%b%p%% %t]")
;; (display-battery-mode)
#+end_src

** Ag
#+BEGIN_SRC emacs-lisp
  (ensure-pkgs '(ag wgrep-ag))
  (setq ag-highlight-search t
        ag-reuse-buffers t)
#+END_SRC
** Dired and Dired Extensions
#+begin_src emacs-lisp
  (require 'dired-x)
  (setq dired-omit-files-p t)
  (add-hook 'dired-mode-hook (lambda () (dired-omit-mode)))
  (setq dired-omit-files (concat dired-omit-files "\\|^\\..+"))

  ;; From What the emacs.d
  ;; Make dired less verbose
  (ensure-pkgs '(dired-details dired+))
  (require 'dired-details)
  (setq-default dired-details-hidden-string "")
  (dired-details-install)
#+end_src
*** Dired and find
#+begin_src emacs-lisp
  (require 'find-dired)
  (setq find-ls-option '("-print0 | xargs -0 ls -ld " . "-ld"))
#+end_src

** Net Utilities
From [[http://irreal.org/blog/?p%3D2247][irreal]]
#+begin_src emacs-lisp
  (setq ping-program-options '("-c" "4"))
  (defun net-utils-restore-windows ()
    "Restore windows and clean up after ping."
    (interactive)
    (kill-buffer (current-buffer))
    (jump-to-register :net-utils-fullscreen))

  (defadvice net-utils-run-program (around net-utils-big-page activate)
    (window-configuration-to-register :net-utils-fullscreen)
    (let ((buf ad-do-it))
      (switch-to-buffer buf)
      (delete-other-windows)
      (set-temporary-overlay-map
        (let ((map (make-sparse-keymap)))
          (define-key map (kbd "q") 'net-utils-restore-windows)
          map))
      (message "Type \"q\" to restore other windows.")))
#+end_src

** Ham Mode: Edit html as markdown
#+begin_src emacs-lisp
  (ensure-pkg 'ham-mode)
#+end_src
* Programming modes
** Editorconfig
#+begin_src emacs-lisp
  (ensure-pkg 'editorconfig)
  (require 'editorconfig)
#+end_src
** Indent settings
Don't use tabs, default to 4 spaces.
#+begin_src emacs-lisp
(setq-default indent-tabs-mode nil)
(setq-default tab-width 4)
(defvaralias 'c-basic-offset 'tab-width)
(defvaralias 'cperl-indent-level 'tab-width)
#+end_src
** Line length
#+begin_src emacs-lisp
  (ensure-pkg 'column-enforce-mode)
  (setq column-enforce-column 120)
#+end_src

** Smartparens
#+begin_src emacs-lisp
  (ensure-pkgs '(smartparens))
  (smartparens-global-mode t)
  (require 'smartparens-config)
  (sp-use-smartparens-bindings)
  (show-smartparens-global-mode t)
  (require 'smartparens-python)
#+end_src
** Lisps
*** Paredit                                                        :disabled:
#+begin_src emacs-lisp
  ;; ;; Paredit
  ;; (mapc (lambda (mode)
  ;;         (let ((hook (intern (concat (symbol-name mode)
  ;;                                     "-mode-hook"))))
  ;;           (add-hook hook (lambda () (paredit-mode +1)))))
  ;;       '(emacs-lisp lisp inferior-lisp))
#+end_src
*** Elisp slime nav                                                :disabled:
Adds M-* and M-, to elisp buffers.
#+begin_src emacs-lisp :noweb-ref my-pkg :exports none :tangle no
  ;; (:name elisp-slime-nav
  ;;        :type github
  ;;        :pkgname "purcell/elisp-slime-nav")
#+end_src

#+begin_src emacs-lisp
  ;; (add-hook 'emacs-lisp-mode-hook (lambda () (elisp-slime-nav-mode t)))
#+end_src

** Projectile
Possible fit for project management.
#+begin_src emacs-lisp
  (ensure-pkg 'projectile)
  (setq projectile-enable-caching nil)
  (projectile-global-mode)
  (setq projectile-switch-project-action 'projectile-find-dir)
  (ensure-pkg 'helm-projectile)
  (require 'helm-projectile)
  (helm-projectile-on)
  (define-key projectile-mode-map (kbd "C-c p h") 'helm-projectile)
  (ensure-pkg 'helm-ag)
#+end_src
** Flycheck                                                        :disabled:
#begin_src emacs-lisp
  (ensure-pkg 'flycheck)
  (add-hook 'after-init-hook #'global-flycheck-mode)
  ;; Elisp fixes
  (eval-after-load 'flycheck (lambda () (setq-default flycheck-disabled-checkers '(emacs-lisp-checkdoc))))
#end_src
** Flymake                                                         :disabled:
#begin_src emacs-lisp
;; (require 'flymake-cursor)
#end_src

#begin_src emacs-lisp :noweb-ref my-pkg :exports none :tangle no
;;  (:name flymake-shell
;;         :type github
;;         :pkgname "purcell/flymake-shell")
#end_src
** Tags
#+begin_src emacs-lisp
(setq tags-revert-without-query t)
#+end_src
** Yasnippet
#+begin_src emacs-lisp
  (ensure-pkgs '(yasnippet))
  (yas-global-mode 1)
  (yas-load-directory "~/.emacs.d/snippets" t)
#+end_src
*** Helm
#+begin_src emacs-lisp
  (ensure-pkg 'helm-c-yasnippet)
  (define-key yas-minor-mode-map (kbd "C-c & s") 'helm-yas-complete)
  (define-key yas-minor-mode-map (kbd "C-c & v") 'helm-yas-visit-snippet-file)
  (define-key yas-minor-mode-map (kbd "C-c & n") 'helm-yas-create-snippet-on-region)
#+end_src
** Groovy
#+begin_src emacs-lisp
  (ensure-pkgs '(groovy-mode))
  (autoload 'groovy-mode "groovy-mode" "Major mode for editing Groovy code." t)
  (add-to-list 'auto-mode-alist '("\.groovy$" . groovy-mode))
  (add-to-list 'interpreter-mode-alist '("groovy" . groovy-mode))
  (add-to-list 'auto-mode-alist '("\.gradle$" . groovy-mode))

  ;;; make Groovy mode electric by default.
  (add-hook 'groovy-mode-hook
            '(lambda ()
               (require 'groovy-electric)
               (setq tab-width 2)
               (groovy-electric-mode)))
#+end_src

** Imenu
#+begin_src emacs-lisp
  (ensure-pkgs '(imenu-anywhere))
  (setq-default imenu-generic-expression '(nil))
  (global-set-key (kbd "C-.") 'imenu-anywhere)

  ;; Add a mark to pop back to
  (defadvice imenu-anywhere (before push-mark activate)
    (push-mark))
#+end_src
** Global whitespace cleanup
From http://stackoverflow.com/questions/3533703/emacs-delete-trailing-whitespace-except-current-line
#begin_src emacs-lisp
  (defun delete-trailing-whitespace-except-current-line ()
    (interactive)
    (let ((begin (line-beginning-position))
          (end (line-end-position)))
      (save-excursion
        (when (< (point-min) begin)
          (save-restriction
            (narrow-to-region (point-min) (1- begin))
            (delete-trailing-whitespace)))
        (when (> (point-max) end)
          (save-restriction
            (narrow-to-region (1+ end) (point-max))
            (delete-trailing-whitespace)))
        (widen)
        (goto-char (point-max))
        (delete-blank-lines)
        (let ((trailnewlines (abs (skip-chars-backward "\n\t"))))
          (if (> trailnewlines 1)
              (progn
                (delete-char (- trailnewlines1 ))))))))
#end_src
From "What the emacs.d!?"
#begin_src emacs-lisp
  (defun cleanup-buffer-safe ()
    "Perform a bunch of safe operations on the whitespace content of a buffer.
  Does not indent buffer, because it is used for a before-save-hook, and that
  might be bad."
    (interactive)
    (untabify (point-min) (point-max))
    (delete-trailing-whitespace-except-current-line)
    (set-buffer-file-coding-system 'utf-8))

  ;; Various superfluous white-space. Just say no.
  (add-hook 'before-save-hook 'cleanup-buffer-safe)
#end_src

Let's try ws-butler instead:
#+begin_src emacs-lisp
  (ensure-pkg 'ws-butler)
  (ws-butler-global-mode)
#+end_src
** Python
#+begin_src emacs-lisp
  (ensure-pkg 'elpy)
  (elpy-enable)

  (ensure-pkg 'sphinx-doc)
  (defun my-elpy-setup ()
    (require 'sphinx-doc)
    (sphinx-doc-mode t)
    (message "Done with custom setup."))
  (add-hook 'elpy-mode-hook 'my-elpy-setup)
#+end_src
** Old Python!                                                     :disabled:
Use elpy, and tweak indentation.
#begin_src emacs-lisp
  (ensure-pkgs '(yasnippet flymake-cursor elpy))
  (elpy-enable)
  ;(elpy-use-ipython)
  (setq elpy-rpc-backend "jedi")

  ; Fix yas-snippet-dirs stealing
  (setq yas-snippet-dirs (cons "~/.emacs.d/snippets" yas-snippet-dirs))
  (defun my-python-hook ()
    (setq tab-width 2)
    (setq python-indent 2)
    (setq-local helm-dash-docsets '("Python 2" "Flask")))
  (add-hook 'python-mode-hook 'my-python-hook)
#end_src
*** TODO Eshell support for virtualenvs
Work in progress.
#+begin_src emacs-lisp
  (defun eshell/workon (virtualenv)
    (let ((virtualenv-workon-starts-python nil))
      (virtualenv-workon virtualenv)
      (setq exec-path (split-string (getenv "PATH") ":"))))
#+end_src

** HTML and Jinja
   #begin_src emacs-lisp
  (ensure-pkgs '(web-mode))
  (require 'web-mode)
  (add-to-list 'auto-mode-alist '("\\.html?\\'" . web-mode))
  (setq web-mode-engines-alist '(("jinja2"    . "\\.html?\\'")))
  (when-installed 'smartparens (sp-local-tag '(web-mode) "<" "<_>" "</_>" :transform 'sp-match-sgml-tags))
#end_src
*** TODO Emmet mode!
Research ac-emmet?
#begin_src emacs-lisp
  (ensure-pkg 'emmet-mode)
  (require 'emmet-mode)
  (add-hook 'sgml-mode-hook 'emmet-mode)
  (add-hook 'web-mode-hook 'emmet-mode)
  (add-hook 'css-mode-hook  'emmet-mode)
  (add-hook 'emmet-mode-hook (lambda () (setq emmet-indentation 2)))
  (setq emmet-move-cursor-between-quotes t)
#end_src
** Javascript
#begin_src emacs-lisp
  (ensure-pkgs '(js2-mode))
  (add-to-list 'auto-mode-alist '("\\.js\\'" . js2-mode))
  (add-to-list 'auto-mode-alist '("\\.html?\\'" . web-mode))
  (add-hook 'js2-mode-hook
            (lambda ()
              (setq tab-width 2)
              (setq js2-basic-offset 2)
              (add-hook 'before-save-hook 'delete-trailing-whitespace nil t)))
#end_src
*** Skewer mode                                                    :disabled:
Only useful for javascript and css, as HTML changes don't get reflected.
The missing pieces should be filled in by LiveReload...
#begin_src emacs-lisp
  ;(ensure-pkg 'skewer-mode)
  ;(add-hook 'js2-mode-hook 'skewer-mode)
  ;; If we need it for css/html/web-mode:
  ;(add-hook 'css-mode-hook 'skewer-css-mode)
  ;(add-hook 'html-mode-hook 'skewer-html-mode)
  ;(add-hook 'web-mode-hook 'skewer-html-mode)
#end_src

Snippet to load javascript:
#+BEGIN_SRC html
<script src="http://localhost:8080/skewer"/>
#+END_SRC
* Org-mode Setup
** Require
#+begin_src emacs-lisp
(ensure-pkg 'org-plus-contrib)
(require 'org)
;(require 'org-protocol)
#+end_src
** Bindings
*** Speed keys
From worg: http://orgmode.org/worg/org-hacks.html
#+begin_src emacs-lisp
  (defun ded/org-show-next-heading-tidily ()
    "Show next entry, keeping other entries closed."
    (if (save-excursion (end-of-line) (outline-invisible-p))
        (progn (org-show-entry) (show-children))
      (outline-next-heading)
      (unless (and (bolp) (org-on-heading-p))
        (org-up-heading-safe)
        (hide-subtree)
        (error "Boundary reached"))
      (org-overview)
      (org-reveal t)
      (org-show-entry)
      (show-children)))

  (defun ded/org-show-previous-heading-tidily ()
    "Show previous entry, keeping other entries closed."
    (let ((pos (point)))
      (outline-previous-heading)
      (unless (and (< (point) pos) (bolp) (org-on-heading-p))
        (goto-char pos)
        (hide-subtree)
        (error "Boundary reached"))
      (org-overview)
      (org-reveal t)
      (org-show-entry)
      (show-children)))

  (setq org-use-speed-commands t)
  (add-to-list 'org-speed-commands-user
               '("n" ded/org-show-next-heading-tidily))
  (add-to-list 'org-speed-commands-user
               '("p" ded/org-show-previous-heading-tidily))
#+end_src
*** Capture
#+begin_src emacs-lisp
(global-set-key "\C-cl" 'org-store-link)
(global-set-key "\C-cc" 'org-capture)
(global-set-key "\C-ca" 'org-agenda)
(global-set-key "\C-cb" 'org-iswitchb)
#+end_src
** Configure
#+begin_src emacs-lisp
  (setq org-completion-use-ido t
        org-special-ctrl-a/e t
        org-special-ctrl-k t
        org-yank-adjusted-subtrees t
        org-enforce-todo-checkbox-dependencies t
        org-enforce-todo-dependencies t
        org-default-notes-file "~/org/notes.org"
        org-log-into-drawer t
        org-clock-into-drawer t
        org-clock-out-remove-zero-time-clocks t
        org-use-fast-todo-selection t
        org-agenda-start-on-weekday nil
        org-use-speed-commands t
        org-treat-S-cursor-todo-selection-as-state-change nil)
#+end_src
** Keywords and to-dos
#+begin_src emacs-lisp
(setq org-todo-keywords '((sequence "TODO(t)" "DONE(d)")))
#+end_src
** Capture Templates
#+begin_src emacs-lisp
  ;; Inspiration from http://doc.norang.ca/org-mode.html#Capture
  (setq org-capture-templates `(("t" "todo" entry (file ,org-default-notes-file)
                                 "* TODO %?\n%U\n%a\n" :clock-in t :clock-resume t)
                                ("e" "email" entry (file ,org-default-notes-file)
                                 "* TODO Email: %a\nSCHEDULED: %t\n%U\n" :clock-in t :clock-resume t :immediate-finish t)
                                ("l" "log" entry (file+datetree ,org-default-notes-file)
                                 "* %?\n%U\n" :clock-in t :clock-resume t)
                                ("i" "Interruption" entry (file+datetree ,org-default-notes-file)
                                 "* %? :interruption:\n%U\n" :clock-in t :clock-resume t)
                                ("m" "Meeting" entry (file ,org-default-notes-file)
                                 "* Meeting: %?\n%T\n" :clock-in t :clock-resume t)
                                ("h" "Habit" entry (file ,org-default-notes-file)
                                 "* TODO %?\n%U\n%a\nSCHEDULED: %(format-time-string \"<%Y-%m-%d %a .+1d/3d>\")\n:PROPERTIES:\n:STYLE: habit\n:REPEAT_TO_STATE: TODO\n:END:\n")
                                ))

  ;; Cleanup empty clocks, also from norang:
  ;; Remove empty LOGBOOK drawers on clock out
  (defun bh/remove-empty-drawer-on-clock-out ()
    (interactive)
    (save-excursion
      (beginning-of-line 0)
      (org-remove-empty-drawer-at "LOGBOOK" (point))))

  (add-hook 'org-clock-out-hook 'bh/remove-empty-drawer-on-clock-out 'append)
#+end_src
** Org Modules
*** Org-Velocity
#+begin_src emacs-lisp
  (require 'org-velocity)
  (global-set-key (kbd "C-c v") 'org-velocity-read)
  (setq org-velocity-bucket "~/org/velocity.org"
        org-velocity-always-use-bucket t
        org-velocity-exit-on-match nil)
#+end_src
*** Org Pomodoro
M-x org-pomodoro
#+begin_src emacs-lisp
  (ensure-pkgs '(org-pomodoro))
  (require 'org-pomodoro)
  (global-set-key (kbd "C-c p") 'org-pomodoro)
#+end_src
*** MobileOrg
#begin_src emacs-lisp
  (setq org-mobile-directory "~/.MobileOrg")

  ;; From stackoverflow:

  (defvar my-org-mobile-sync-timer nil)

  (defvar my-org-mobile-sync-secs (* 60 2)) ;; Sync every two minutes

  (defun my-org-mobile-sync-pull-and-push ()
    (interactive)
    (org-mobile-pull)
    (org-mobile-push))

  (defun my-org-mobile-sync-start ()
    "Start automated `org-mobile-push'"
    (interactive)
    (setq my-org-mobile-sync-timer
          (run-with-idle-timer my-org-mobile-sync-secs t
                               'my-org-mobile-sync-pull-and-push)))

  (defun my-org-mobile-sync-stop ()
    "Stop automated `org-mobile-push'"
    (interactive)
    (cancel-timer my-org-mobile-sync-timer))

  (my-org-mobile-sync-start)
#end_src
** Org Babel / Code Blocks
Enable python.
#+begin_src emacs-lisp
  (add-to-list 'org-babel-load-languages '("python" . t))
  (org-babel-do-load-languages
   'org-babel-load-languages
   '((python . t)))
#+end_src
** Org Trello
#+begin_src emacs-lisp
  (ensure-pkg 'org-trello)
  (require 'org-trello)
#+end_src
** Defunct                                                         :disabled:
*** Org Mode notifications

(require 'appt)
(setq appt-message-warning-time 15
      appt-display-mode-line t
      appt-display-format 'window)
(appt-activate 1)
(display-time)

(org-agenda-to-appt t)
(add-hook 'org-finalize-agenda-hook 'org-agenda-to-appt)

*** Stay on task (Idle display of Agenda)

   ;; From http://article.gmane.org/gmane.emacs.orgmode/23047
   (defun jump-to-org-agenda ()
     (interactive)
     (let ((buf (get-buffer "*Org Agenda*"))
           wind)
       (if buf
           (if (setq wind (get-buffer-window buf))
               (select-window wind)
             (if (called-interactively-p)
                 (progn
                   (select-window (display-buffer buf t t))
                   (org-fit-window-to-buffer)
                   ;; (org-agenda-redo)
                   )
               (with-selected-window (display-buffer buf)
                 (org-fit-window-to-buffer)
                 ;; (org-agenda-redo)
                 )))
         (call-interactively 'org-agenda-list)))
     ;;(let ((buf (get-buffer "*Calendar*")))
     ;;  (unless (get-buffer-window buf)
     ;;    (org-agenda-goto-calendar)))
     )
   (let ((timer (timer-create)))
     (timer-set-function timer 'jump-to-org-agenda)
     (timer-set-idle-time timer 300 t)
     (timer-activate-when-idle timer nil))
   ;;(run-with-idle-timer 300 t 'jump-to-org-agenda)

*** Export
**** Dark backgrounds for code blocks

;; (setq org-export-html-style
;;       "<style type=\"text/css\">
;; <!--/*--><![CDATA[/*><!--*/
;; pre.src { color: #f6f3e8 !important; background-color: #242424 !important; }
;; /*]]>*/-->
;; </style>")

* ERC
Using bouncer.
#+begin_src emacs-lisp
  (load-file (expand-file-name "~/.emacs.d/secrets.el"))
  (defun my-erc ()
    (interactive)
    (erc
      :server "localhost"
      :port "6667"
      :nick my-erc-name
      :password my-erc-password))
  (setq erc-track-exclude-types '("JOIN" "NICK" "PART" "QUIT" "MODE" "324" "329" "332" "333" "353" "477"))

#+end_src

* Evil                                                             :disabled:
#begin_src emacs-lisp
  (ensure-pkgs '(evil))
  ; http://puntoblogspot.blogspot.com/2014/01/evil-exact-amount-of-vim-in-emacs-but.html
  (require 'evil)
  ;(setcdr evil-insert-state-map nil)
  ;(define-key evil-insert-state-map [escape] 'evil-normal-state)
#end_src
** Evil Extensions                                                 :disabled:
#begin_src emacs-lisp
  (ensure-pkg 'evil-matchit)
  (require 'evil-matchit)
  (global-evil-matchit-mode 1)

  (ensure-pkg 'evil-nerd-commenter)
  (require 'evil-nerd-commenter)
  (global-set-key (kbd "M-;") 'evilnc-comment-or-uncomment-lines)

  (ensure-pkg 'surround)
  (require 'surround)
  (global-surround-mode 1)
#end_src
* Stuff that happens at the end
** Clean Modeline with Diminish                                    :disabled:
#begin_src emacs-lisp
  (ensure-pkg 'diminish)
  (diminish 'helm-mode)
  (diminish 'auto-revert-mode)
  (diminish 'magit-wip-save-mode)
  (diminish 'smartparens-mode)
  (diminish 'projectile-mode)
  ;;(diminish 'undo-tree-mode)
  (diminish 'highlight-indentation-mode)
#end_src
* Custom File
#+begin_src emacs-lisp
(setq custom-file (expand-file-name "~/.emacs.d/custom.el"))
(load custom-file)
#+end_src
